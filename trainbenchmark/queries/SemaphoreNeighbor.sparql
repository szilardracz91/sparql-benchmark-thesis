PREFIX base: <http://www.semanticweb.org/ontologies/2015/trainbenchmark#>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?semaphore ?route1 ?route2 ?sensor1 ?sensor2 ?te1 ?te2
WHERE
{
	?route1 base:exit ?semaphore .
	?route1 base:definedBy ?sensor1 .
	?te1 base:sensor ?sensor1 .
	?te1 base:connectsTo ?te2 .
	?te2 base:sensor ?sensor2 .
	?route2 base:definedBy ?sensor2 .

	?semaphore rdf:type base:Semaphore .
	?route1 rdf:type base:Route .
	?route2 rdf:type base:Route .
	?sensor1 rdf:type base:Sensor .
	?sensor2 rdf:type base:Sensor .
	?te1 rdf:type base:TrackElement .
	?te2 rdf:type base:TrackElement .

	FILTER (?route1 != ?route2) .

	OPTIONAL {
		?route2 ?entry ?semaphore .
		FILTER (sameTerm(base:entry, ?entry))
	} .
	FILTER (!bound(?entry))
}
